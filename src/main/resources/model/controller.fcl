FUNCTION_BLOCK controller	// Definicja bloku (moze byc wiecej blokow w pliku)

VAR_INPUT				// Definiuje zmienne wejsciowe
	wilgotnosc : REAL;
	temperatura : REAL;
	jakosc : REAL;
END_VAR

VAR_OUTPUT				// Definiuje zmienne wyjsciowe
	priorytet : REAL;
END_VAR

FUZZIFY wilgotnosc
	TERM mala := (15, 1) (30, 0) ;
	TERM srednia := (20, 0) (35, 1) (45, 1) (65,0);
	TERM duza := (55, 0) (75, 1);
END_FUZZIFY

FUZZIFY temperatura
	TERM niska := (0, 1) (12, 0) ;
	TERM srednia := (10,0) (14,1) (20,1) (27,0);
	TERM wysoka := (25,0) (30,1);
END_FUZZIFY

FUZZIFY jakosc
	TERM slaba := (15, 1) (30, 0) ;
    TERM srednia := (20, 0) (35, 1) (45, 1) (65,0);
   	TERM dobra := (55, 0) (75, 1);
END_FUZZIFY

DEFUZZIFY priorytet
	TERM niski := (0,0) (20,1) (40,0);
	TERM sredni := (30,0) (50,1) (60,0);
	TERM wysoki := (50,0) (70,1) (90,0);
	TERM bardzo_wysoki := (80,0) (95,1) (100,1);
	
	METHOD : COG;		// Metoda defuzyfikacji - 'Center Of Gravity' - 'Sreodka ciezkosci'
	DEFAULT := 0;		
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Uzycie 'min' jako 'and' (implikuje uzycie 'max' jako 'or' dla zapewnienia praw DeMorgan'a)
	ACT : MIN;			// Uzycie  'min' jako metody aktywacji
	ACCU : MAX;			// Metoda agregacji - 'max'

	RULE 1 : IF wilgotnosc IS mala AND temperatura IS wysoka AND jakosc IS slaba THEN priorytet IS bardzo_wysoki;
	RULE 2 : IF wilgotnosc IS mala OR temperatura IS wysoka OR jakosc IS slaba THEN priorytet IS wysoki;
	RULE 3 : IF wilgotnosc IS srednia OR temperatura IS srednia OR jakosc IS srednia THEN priorytet IS sredni;
	RULE 4 : IF wilgotnosc IS duza AND temperatura IS srednia AND jakosc IS dobra THEN priorytet IS niski;
	RULE 5 : IF wilgotnosc IS srednia AND temperatura IS niska OR jakosc IS slaba THEN priorytet IS sredni;

END_RULEBLOCK

END_FUNCTION_BLOCK

