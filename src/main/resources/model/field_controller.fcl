FUNCTION_BLOCK field_controller	// Definicja bloku (moze byc wiecej blokow w pliku)

VAR_INPUT				// Definiuje zmienne wejsciowe
	yields : REAL;
	minerals : REAL;
	weeds : REAL;
END_VAR

VAR_OUTPUT				// Definiuje zmienne wyjsciowe
	priority : REAL;
END_VAR

FUZZIFY yields
	TERM little := (15, 1) (30, 0) ;
	TERM middle := (20, 0) (35, 1) (45, 1) (65,0);
	TERM much := (55, 0) (75, 1);
END_FUZZIFY

FUZZIFY minerals
	TERM little := (0, 1) (12, 0) ;
	TERM middle := (10,0) (14,1) (20,1) (27,0);
	TERM much := (25,0) (30,1);
END_FUZZIFY

FUZZIFY weeds
	TERM little := (15, 1) (30, 0) ;
    TERM middle := (20, 0) (35, 1) (45, 1) (65,0);
   	TERM much := (55, 0) (75, 1);
END_FUZZIFY

DEFUZZIFY priority
	TERM low := (0,1) (20,1) (40,0);
	TERM middle := (30,0) (50,1) (60,0);
	TERM high := (50,0) (70,1) (85,0);
	TERM very_high := (70,0) (85,1) (100,1);

	METHOD : COG;		// Metoda defuzyfikacji - 'Center Of Gravity' - 'Sreodka ciezkosci'
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Uzycie 'min' jako 'and' (implikuje uzycie 'max' jako 'or' dla zapewnienia praw DeMorgan'a)
	ACT : MIN;			// Uzycie  'min' jako metody aktywacji
	ACCU : MAX;			// Metoda agregacji - 'max'

	RULE 1 : IF yields IS much AND weeds is much THEN priority IS very_high;
	RULE 2 : IF yields IS much AND minerals is little THEN priority IS very_high;
	RULE 3 : IF weeds IS much AND minerals is little THEN priority IS very_high;

	RULE 4 : IF weeds IS much AND minerals is middle THEN priority IS high;
	RULE 5 : IF yields IS much AND minerals is middle THEN priority IS high;
	RULE 6 : IF weeds IS much AND yields is middle THEN priority IS high;
	RULE 7 : IF yields IS much AND weeds is middle THEN priority IS high;
	RULE 8 : IF minerals IS little AND weeds is middle THEN priority IS high;
	RULE 9 : IF minerals IS little AND yields is middle THEN priority IS high;

	RULE 10 : IF minerals IS little OR yields IS much OR weeds IS much THEN priority IS middle;

	RULE 11 : IF yields IS middle AND weeds is middle THEN priority IS low;
    RULE 12 : IF yields IS middle AND minerals is middle THEN priority IS low;
    RULE 13 : IF weeds IS middle AND minerals is middle THEN priority IS low;

    RULE 14 : IF yields IS little OR yields IS middle OR yields IS much OR minerals IS little OR minerals IS middle
    OR minerals IS much OR weeds IS little OR weeds IS middle OR weeds IS much THEN priority IS low;

END_RULEBLOCK

END_FUNCTION_BLOCK